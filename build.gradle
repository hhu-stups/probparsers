apply plugin: 'java-library'
apply plugin: 'eclipse'

// group and version are also copied to all subprojects (see below)
project.group = 'de.hhu.stups'
project.version = '2.9.24-SNAPSHOT'

final SOURCE_ENCODING = "UTF-8"

wrapper {
    gradleVersion = "6.2.1"
    distributionType = Wrapper.DistributionType.ALL
}

ext {
    sableCCDependency = [group: 'de.hhu.stups', name: 'sablecc', version: '3.2.13']
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    
    group = rootProject.group
    version = rootProject.version
    
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    
    repositories {
        mavenCentral()
        maven {
            name "snapshots"
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    
    tasks.withType(JavaCompile) {
        options.encoding = SOURCE_ENCODING
    }
    
    tasks.withType(Javadoc) {
        options.encoding = SOURCE_ENCODING
        // Workaround for https://github.com/gradle/gradle/issues/5630
        // See https://stackoverflow.com/a/52797107
        options.addStringOption("sourcepath", "")
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    dependencies {
        testImplementation group: "junit", name: "junit", version: "4.13"
    }

    if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {

        println "Configuring deployment for ${project.name}"

        apply plugin: 'signing'

        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar, sourcesJar
        }


        signing {
            sign configurations.archives
        }

        uploadArchives {
            repositories {
                mavenDeployer {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    pom.project {
                        name project.getName()
                        packaging 'jar'
                        // optionally artifactId can be defined here
                        description 'Part of the ProB Parser library'
                        url 'https://github.com/bendisposto/probparsers'

                        licenses {
                            license {
                                name 'Eclipse Public License, Version 1.0'
                                url 'http://www.eclipse.org/org/documents/epl-v10.html'
                            }
                        }

                        scm {
                            connection 'scm:git:git://gitlab.cs.uni-duesseldorf.de/stups/prob/probparsers.git'
                            developerConnection 'scm:git:git@github.com:hhu-stups/probparsers.git'
                            url 'https://gitlab.cs.uni-duesseldorf.de/stups/prob/probparsers'
                        }


                        developers {
                            developer {
                                id 'bendisposto'
                                name 'Jens Bendisposto'
                                email 'jens@bendisposto.de'
                            }
                            developer {
                                id 'leuschel'
                                name 'Michael Leuschel'
                                email 'leuschel@hhu.de'
                            }
                        }
                    }
                }
            }
        }
    }

}

task uberjar(type: Jar, dependsOn: subprojects.assemble) {
    archiveBaseName.set('probcliparser')
    subprojects.each { subproject ->
        from subproject.configurations.archives.allArtifacts.getFiles().collect { zipTree(it) }
    }
    manifest { attributes 'Main-Class': 'de.prob.cliparser.CliBParser' }
}

ext {
    // This closure is called in each subproject that wants test reports generated.
    setupTestReport = {Project subproject, List<String> sableCCPackageNames -> void
        final jacocoTestReportTask = subproject.tasks.named("jacocoTestReport")
        jacocoTestReportTask.configure {JacocoReport task ->
            task.reports {
                xml.enabled = false
                csv.enabled = false
                html.destination = new File(subproject.buildDir, "jacocoHtml")
            }
            
            task.classDirectories.from = task.classDirectories.from.collect {
                final exclusions = ["**/de/hhu/stups/sablecc/patch/**"]
                sableCCPackageNames.each {packageName ->
                    final packagePath = packageName.replace(".", "/")
                    exclusions.addAll([
                        "**/${packagePath}/analysis/**",
                        "**/${packagePath}/lexer/**",
                        "**/${packagePath}/node/**",
                        "**/${packagePath}/parser/**",
                    ])
                }
                it.asFileTree.matching {
                    exclude(exclusions)
                }
            }
        }
    }
}

task deploy {
    doLast {
        throw new GradleException(
            """\
            The deploy task has been removed. Please use one of the following tasks instead, depending on what you want to do:
            To build the parser jar files: ./gradlew assemble
            To run the tests: ./gradlew check
            To build and test the parser: ./gradlew build
            To generate test coverage reports: ./gradlew check jacocoTestReport
            """.stripIndent()
        )
    }
}
